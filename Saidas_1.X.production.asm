  ; DESENVOLVIDO POR EVERTON P. CRUZ
  ; COM O PROPOSITO DE USO BASE PARA EXEMPLOS SOBRE O SIMULIDE
  #INCLUDE <P16F628A.INC>
  LIST P=16F628A
  __CONFIG _BOREN_ON & _CP_OFF & _PWRTE_ON & _WDT_OFF & _LVP_OFF & _MCLRE_OFF & _INTRC_OSC_NOCLKOUT
  
  #DEFINE d1 0x20
  #DEFINE d2 0x21
  #DEFINE d3 0x22
    
  ORG     0x00       ; TRATAMENTO DO RESET
  GOTO    SETUP_PROCESSO

  ORG     0x04       ; TRATAMENTO DAS INTERRUPCOES
  RETFIE
    
SETUP_PROCESSO:
  BCF     STATUS, RP1
  BSF     STATUS, RP0 ; SELECIONA O BANCO 1 DE MEMORIA

  BSF	PCON, OSCF    ; CONF. FREQÜÊNCIA DO OSCILADOR INTERNO EM 4 MHz
  
  MOVLW	B'10000000'
  ; 1 - NOT_RBPU    -> DESATIVA RESISTORES PULL-UP
  ; 0 - INTDEG      -> INT. ATIV. BORDA DE DESCIDA RB0
  ; 0 - TOCS        -> O CLOCK DO TIMER 0 EH INTERNO
  ; 0 - TOSE        -> BORDA DE SUBIDA NO TIMER 0, RA4
  ; 0 - PSA         -> PRESCALER ASSOCIADO AO TIMER 0
  ; 0 - PS2,PS1,PS0 -> TIMER 0 COM RAZAO 1:2
  MOVWF   OPTION_REG ; CONFIGURACOES DO REGISTRADOR OPTION
    
  MOVLW   B'00000000'
  ; 0 - PINO RA7 COMO SAIDA
  ; 0 - PINO RA6 COMO SAIDA
  ; 0 - PINO RA5 COMO SAIDA
  ; 0 - PINO RA4 COMO SAIDA
  ; 0 - PINO RA3 COMO SAIDA
  ; 0 - PINO RA2 COMO SAIDA
  ; 0 - PINO RA1 COMO SAIDA
  ; 0 - PINO RA0 COMO SAIDA
  MOVWF   TRISA ; MODIFICA O COMPORTAMENTO DA PORTA
    
  MOVLW   B'00000000'
  ; 0 - PINO RB7 COMO SAIDA
  ; 0 - PINO RB6 COMO SAIDA
  ; 0 - PINO RB5 COMO SAIDA
  ; 0 - PINO RB4 COMO SAIDA
  ; 0 - PINO RB3 COMO SAIDA
  ; 0 - PINO RB2 COMO SAIDA
  ; 0 - PINO RB1 COMO SAIDA
  ; 0 - PINO RB0 COMO SAIDA
  MOVWF   TRISB ; MODIFICA O COMPORTAMENTO DA PORTB    
    
  BCF     STATUS, RP1
  BCF     STATUS, RP0 ; SELECIONA O BANCO 0 DE MEMORIA
    
  MOVLW   B'00000111'
  MOVWF   CMCON ; DEFINE O MODO DE OPERACAO DO COMP. ANALOG. DESLIGADOS
    
  MOVLW   B'00000000'  
  ; 0 - GIE  -> DESATIVA INT./CHAVE GERAL
  ; 0 - EEIE -> DESATIVA INT. DA EEPROM
  ; 0 - T0IE -> DESATIVA INT. NO TMR0
  ; 0 - INTE -> DESATIVA INT. NO RB0/INT
  ; 0 - RBIE -> DESATIVA INT. MUDANCA RB<7:4>
  ; 0 - T0IF -> FLAG DE TRANSBORDO DO TMR0
  ; 0 - INTF -> FLAG DE RESPOSTA INT. RB0/INT
  ; 0 - RBIF -> FLAG DE RESPOSTA INT EM RB<7:4>
  MOVWF   INTCON ; CONFIGURACOES DE INTERRUPCOES

  BCF     STATUS, C
PROGRAMAPRINCIPAL:
  ; INICIO DO PROCESSO
  CLRF    PORTA

REPETICAO_COLUNA:
  CLRF    PORTB
  BSF     PORTB, RB0
  CALL    DELAY_1_SECOND
  
  RLF     PORTB, 1
  CALL    DELAY_1_SECOND
  
  RLF     PORTB, 1
  CALL    DELAY_1_SECOND
  
  RLF     PORTB, 1
  CALL    DELAY_1_SECOND
  
  RLF     PORTB, 1
  CALL    DELAY_1_SECOND
  
  RLF     PORTB, 1
  CALL    DELAY_1_SECOND
  
  RLF     PORTB, 1
  CALL    DELAY_1_SECOND
  
  RLF     PORTB, 1
  CALL    DELAY_1_SECOND
  
  INCF    PORTA, 1
  
  BTFSS   PORTA, RB3
  GOTO    REPETICAO_COLUNA
  CLRF    PORTA
  
  GOTO    PROGRAMAPRINCIPAL

DELAY_1_SECOND:
  ; http://golovchenko.org/cgi-bin/delay
  ; Delay = 1 seconds
  ; Clock frequency = 4 MHz
  ; Actual delay = 1 seconds = 1000000 cycles
  ; Error = 0 %
  
  ;999997 cycles
  movlw	0x08
  movwf	d1
  movlw	0x2F
  movwf	d2
  movlw	0x03
  movwf	d3
Delay_0:
  decfsz d1, f
  goto   $+2
  decfsz d2, f
  goto   $+2
  decfsz d3, f
  goto   Delay_0
  ;3 cycles
  nop
  return

  END